/*
 * SqlClient Groovy application project
 * For more details on building Java & JVM projects, please refer to https://docs.gradle.org/8.6/userguide/building_java_projects.html in the Gradle documentation.
 */

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'groovy'

    id 'application'

    // want to be able to bundle everything into a single JAR
    id("com.github.johnrengelman.shadow") version "8.1.1"
}

repositories {
    mavenCentral()
    flatDir {
        dirs '/app/d7/lib/extensions/jdbc-drivers/snowflake-1.x', '/app/d7/lib/extensions/jdbc-drivers/vdp-7.0'
    }
}

dependencies {
    implementation 'org.apache.groovy:groovy-all:4.0.19'
    implementation 'org.apache.groovy:groovy-dateutil:4.0.19'
    implementation 'org.apache.groovy:groovy-cli-commons:4.0.19'
    implementation 'org.apache.groovy:groovy-toml:4.0.19'
    implementation 'org.apache.commons:commons-csv:1.10.0'
    implementation 'org.jline:jline:3.25.1'
    implementation 'org.apache.commons:commons-lang3:3.14.0'
    implementation name: 'snowflake-jdbc-3.6.28'
    implementation name: 'denodo-vdp-jdbcdriver'


    // used by gradle (only) to integrate with maven
    implementation libs.guava

    testImplementation 'org.spockframework:spock-core:2.4-M1-groovy-4.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'com.brokenmember.database.SqlClient'
}

tasks.named("test") {
    useJUnitPlatform()
    testLogging {
        events "started", "passed", "skipped", "failed"
    }
}

tasks.register("myConfigurations") {
    doLast {
        configurations.forEach { println(it) }
    }
}

tasks.withType(ShadowJar) {
    mergeGroovyExtensionModules()
}

